if exists (select * from sys.objects where name='sp_SaveACRAQueryResult' and type='P')
	drop procedure dbo.sp_SaveACRAQueryResult
GO

create procedure dbo.sp_SaveACRAQueryResult(@APPLICATION_ID	uniqueidentifier,
											@FICO_SCORE		char(3),
											@RESPONSE_XML	nvarchar(max) = null,
											@DETAILS		dbo.ACRAQueryResultDetails	readonly,
											@QUERIES		dbo.ACRAQueryResultQueries	readonly)
AS
	BEGIN TRANSACTION
	BEGIN TRY
		declare @CURRENT_STATUS tinyint

		select @CURRENT_STATUS = STATUS_ID from dbo.APPLICATION with (updlock) where ID = @APPLICATION_ID

		if @CURRENT_STATUS <> 2
			RAISERROR (N'Հայտի կարգավիճակն արդեն փոփոխվել է', 17, 1)

		insert into dbo.ACRA_QUERY_RESULT
			(APPLICATION_ID,FICO_SCORE,RESPONSE_XML)
		values
			(@APPLICATION_ID,@FICO_SCORE,@RESPONSE_XML)

		delete from dbo.ACRA_QUERY_RESULT_DETAILS where APPLICATION_ID=@APPLICATION_ID
		insert into dbo.ACRA_QUERY_RESULT_DETAILS
			(APPLICATION_ID,STATUS,FROM_DATE,TO_DATE,TYPE,CUR,CONTRACT_AMOUNT,DEBT,PAST_DUE_DATE,RISK,CLASSIFICATION_DATE,
				INTEREST_RATE,PLEDGE,PLEDGE_AMOUNT,OUTSTANDING_AMOUNT,OUTSTANDING_PERCENT,BANK_NAME,IS_GUARANTEE,
				DUE_DAYS_1,DUE_DAYS_2,DUE_DAYS_3,DUE_DAYS_4,DUE_DAYS_5,LAST_REPAYMENT_DATE)
		select @APPLICATION_ID,STATUS,FROM_DATE,TO_DATE,TYPE,CUR,CONTRACT_AMOUNT,DEBT,PAST_DUE_DATE,RISK,CLASSIFICATION_DATE,
				INTEREST_RATE,PLEDGE,PLEDGE_AMOUNT,OUTSTANDING_AMOUNT,OUTSTANDING_PERCENT,BANK_NAME,IS_GUARANTEE,
				DUE_DAYS_1,DUE_DAYS_2,DUE_DAYS_3,DUE_DAYS_4,DUE_DAYS_5,LAST_REPAYMENT_DATE
			from @DETAILS

		delete from dbo.ACRA_QUERY_RESULT_QUERIES where APPLICATION_ID=@APPLICATION_ID
		insert into dbo.ACRA_QUERY_RESULT_QUERIES
			(APPLICATION_ID,DATE,BANK_NAME,REASON)
		select @APPLICATION_ID,DATE,BANK_NAME,REASON
			from @QUERIES

		execute dbo.sp_ChangeApplicationStatus @APPLICATION_ID,3
	END TRY
	BEGIN CATCH
		ROLLBACK TRANSACTION
		declare @ErrorMessage varchar(4000)
		set @ErrorMessage = ERROR_MESSAGE()
		RAISERROR (@ErrorMessage, 17, 1)
		RETURN
	END CATCH
	COMMIT TRANSACTION
GO
